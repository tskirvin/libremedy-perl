#!/usr/bin/perl -T
# Copyright and license are in the documentation below.

##############################################################################
### Configuration
##############################################################################

## Modify and uncomment this to use user code instead of just system-wide
## modules, or to use development versions.
# use lib '/FULL/PATH/TO/PERL/MODULES';
use lib "/afs/ir/users/t/s/tskirvin/work/remedy/Stanford-Remedy/lib";   # not ideal
use lib "/afs/ir/users/t/s/tskirvin/work/libremedy-perl/lib";

## Shared configuration file.  Set with '--config'
our $CONFIG = "/afs/ir/users/t/s/tskirvin/work/libremedy-perl/etc/remedy/config.sandbox";
# our $CONFIG = "/afs/ir/users/t/s/tskirvin/work/libremedy-perl/etc/remedy/config";
# our $CONFIG = "/etc/remedy/config";

## Number of days to go back for the 'unresolved' search. 
our $DAYS = 7;

## Print debugging information?  Set with '--debug'
our $DEBUG = 0;

## Default group information.  Related to, but not set by, '--group'.
our $DEFAULT_GROUP = '';

## Default user information.  Related to, but not set by, '--user'.
our $DEFAULT_USER = $ENV{'REMOTE_USER'} || $ENV{'USER'} || "unknown";

## Default request type, for convenience
our $DEFAULT_TYPE = "base";

## Higher is more verbose ('9' is the highest).  Set to 9 with '--debug'
our $LOGLEVEL = 1;

## Tag to include include in the common log file
our $LOG_TAG = "REMEDY-TKT";

## Default ticket status for searching.
our $STATUS = 'open';

## In the 'time' functions, default number of minutes spent.
our $TIME = 0;

##############################################################################
### Declarations
##############################################################################

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use Remedy;
use Remedy::Person;
use Remedy::Ticket;
use Text::Wrap;

$0 =~ s%.*/%%g;     # Lose the annoying path information
$|++;               # Flush output

use vars qw/$USER $GROUP/;

##############################################################################
### main ()
##############################################################################

my $parser = new Getopt::Long::Parser;
my $result = $parser->getoptions (
    'c|config=s'    => \$CONFIG,
    'time=s'        => \$TIME,
    'days=s'        => \$DAYS,
    'user=s'        => \$USER,
    'group=s'       => \$GROUP,
    'status=s'      => \$STATUS,
    'd|debug'       => sub { $DEBUG = 1; $LOGLEVEL = 9 },
    'man'           => sub { pod2usage (-verbose=>2, -noperldoc => 1) },
    'h|help'        => sub { pod2usage (-verbose=>1) }) || error_usage ();
map { untaint ($_) } $CONFIG, $TIME, $DAYS, $USER, $GROUP, $STATUS;

debug ("Loading config file $CONFIG") if $CONFIG;
my $config = eval { Remedy::Config->load ($CONFIG) };
error ($@) unless $config;
error ($config) if $@;

$config->debug ($DEBUG ? 9 : $LOGLEVEL);

my ($query, @rest) = @ARGV;
$query ||= '(none)';

debug ("Connecting to database");
my $remedy = eval { Remedy->connect ($config) }
    or error ("couldn't connect to database: $@");
error ($@) if $@;

if (lc $query eq 'ticket') { 
    my ($inc, $type, @args) = @rest;
    my $tkt = get_ticket ($remedy, $inc) || error ("no ticket");

    $type ||= '';
    my @list = qw/primary requestor assignee description resolution worklog/;
    if    (lc $type eq 'debug')      { @list = qw/debug/           } 
    elsif (lc $type eq 'audit')      { @list = qw/primary audit/   }
    elsif (lc $type eq 'worklog')    { @list = qw/primary worklog/ }
    elsif (lc $type eq 'timelog')    { @list = qw/primary timelog/ }
    elsif (lc $type eq 'summary')    { @list = qw/primary summary/ }
    elsif ($type =~ /^(all|full)$/i) { push @list, qw/audit time/  }
    else                             { push @list, qw/summary/     }

    print scalar $tkt->print_text (@list);
    exit 0;

} elsif (lc $query eq 'list') { 
    my ($type, @extra) = @rest;

    my %hash = ('user' => $USER, 'group' => $GROUP, 'status' => $STATUS);
    $type ||= 'assign';
    debug ("finding tickets of type '$type'");
    if (lc $type eq 'unresolved') { 
        my $time = time - $DAYS * 86400;
        $hash{'before'} = $time;
        $hash{'days'} = $DAYS;
    } elsif (lc $type eq 'unassigned') { 
        $hash{'unassigned'}++;
    } elsif (lc $type eq 'assign') {
        $hash{'user_assign'} = shift @extra || $USER || $DEFAULT_USER;
    } elsif (lc $type eq 'submit') {
        $hash{'user_submit'} = $USER || $DEFAULT_USER;
    } else { pod2usage (-verbose => 1) }

    foreach my $tkt ($remedy->list (%hash)) { 
        print scalar $tkt->summary_text;
    }
    exit 0;

} elsif (lc $query eq 'assign') { 
    my ($inc, @args) = @rest;
    my $tkt = get_ticket ($remedy, $inc) || error ("no ticket");

    error ("Must set a user (may be blank), a group, or both") 
        unless ($GROUP || defined $USER);
    
    my $return = $tkt->assign ('user' => $USER, 'group' => $GROUP);
    if ($return) { 
        print "success $return\n";
        print scalar $tkt->print_text;
        # print assignee information
    } else { 
        print "failure\n";
    }
    # ...and don't forget to add the 'set status' option!

} elsif (lc $query eq 'time_spent') { 
    my ($inc, $minutes) = @rest;
    my $tkt = get_ticket ($remedy, $inc) || error ("no ticket");

    debug ("Adding timelog information to ticket " . $tkt->inc_num);

    # $tkt->time_spent ($minutes)
    my $timelog = $tkt->timelog_create ();
    $timelog->time_spent ($minutes);
    $timelog->submitter ($USER || $DEFAULT_USER);

    # ...then add it to the main tkt entry as well

    print scalar $timelog->print_text;
    if (eval { my $ret = $timelog ->save }) {
        print "Timelog entry " . $timelog->id . " saved\n";        
        exit 0;
    } else {
        error ("Error saving timelog entry: $@");
    }

} elsif (lc $query eq 'worklog') { 
    my ($inc, $text) = @rest;
    my $incnum = $remedy->parse_incident_number ($inc)
        or pod2usage (-verbose => 1, "Invalid ticket number: $inc");
    pod2usage (-verbose => 1) unless defined $incnum;

    debug ("Adding worklog information to ticket $incnum");
    my $tkt = $remedy->incident ($incnum)
        or $remedy->die_error ("Couldn't load $incnum", $remedy->error);

    my $worklog = $tkt->worklog_create ();

    # all of this data needs a bit of work, but it's reasonably accurate
    $worklog->details ($text);
    $worklog->submitter ($USER || $DEFAULT_USER);
    $worklog->description ("submitted through script '$0'");
    $worklog->type ('General Information');     # probably ought to come from something else

    $worklog->time_spent ($TIME); 
    # care about 'time' (3)?

    print scalar $worklog->print_text;
    if (eval { my $ret = $worklog->save }) {
        print "Worklog entry " . $worklog->id . " saved\n";        
        exit 0;
    } else {
        error ("error saving worklog entry: $@");
    }

} elsif (lc $query eq 'help') {
    pod2usage (-verbose => 2, -exit_status => 0, -noperldoc => 1);

} else { error ("Invalid query type: '$query'") }

exit 0;

##############################################################################
### Subroutines 
##############################################################################

sub debug { warn "$0: @_\n" if $DEBUG }
sub error { my $text = "@_"; chomp $text; die "$0: $text\n" }
sub error_usage { 
    my ($error) = @_;
    pod2usage (-exit_status => 2, -verbose => 1, -message => $error) 
}

sub get_ticket {
    my ($remedy, $inc) = @_;
    my $incnum = $remedy->parse_incident_number ($inc)
        or pod2usage (-verbose => 1, "Invalid ticket number: $inc");
    debug ("Getting information about ticket $incnum");
    my $tkt = $remedy->ticket ($incnum)
        or $remedy->die_error ("Couldn't load $incnum", $remedy->error);
    return $tkt;
}

### _parse_params (TICKET, TYPE)
# Pulls the ticket and printing parameters from the command-line.

sub _parse_params {
    shift @_ if $ENV{'REMOTE_USER'};    # support for remctl use
    my ($incnum, $type) = @_;
    $type ||= "base";
    pod2usage (-verbose => 1) unless ($incnum && $type);

    $type = lc $type;
    pod2usage (-verbose => 1) if $incnum =~ /^(base|full|all|help)$/i;

    # Standardize the ticket name
    unless (($incnum =~ /^(INC|TAS)/) && (length ($incnum) == 15)) {
        my $prefix = $1;
        if ($incnum =~ /^HD\d+/) {
            $incnum =~ s/^HD0//;
            $incnum = $prefix . ('0' x (12 - length ($incnum))) . $incnum;
        } elsif ($incnum =~ /^$prefix\d+/) {
            $incnum =~ s/^$prefix//;
            $incnum = $prefix . ('0' x (12 - length ($incnum))) . $incnum;
        } elsif ($incnum =~ /^\d+$/) {
            $incnum = $prefix . ('0' x (12 - length ($incnum))) . $incnum;
        }
    }

    ($incnum, $type);
}

### untaint (TEXT, MATCH)
# Untaint user-offered data that is "safe enough", or die if it's not.
sub untaint {
    my ($text, $match) = @_;
    $match ||= '[a-zA-Z0-9_./: -]*';
    return '' unless defined $text;
    if ($text =~ m%^($match)$%) { return $1 } 
    else                        { error ("tainted data: '$text' ($match)") }
}

##############################################################################
### Documentation
##############################################################################

=head1 NAME

remedy-ticket - manage remedy tickets

=head1 SYNOPSIS

B<remctl tools remedy ticket> 
  remedy-ticket [-hv] C<INCIDENT_NUMBER> [<base|full|worklog|audit>]

=head1 DESCRIPTION

remedy-ticket queries the Remedy database for information on a given ticket 
(as refrenced with C<INCIDENT_NUMBER> and creates a human-parsable report 
containing all relevant information, including worklog information.  It is 
designed to be used through a remctl interface for the Unix Systems team.

=head1 OPTIONS

=over 4

=item B<-h>, B<--help>

Prints out the synopsis information and exits.

=item B<-v>, B<--version>

Prints out the version information and exits.

=back

=head1 EXAMPLES

There are three basic modes of operation:

=over 4

=item remedy-ticket INC000000804714

=item remedy-ticket INC000000804714 base

Prints basic information about the ticket, the requestor, who it has been
assigned to, resolution information, and a summary of the associated work
log entries.  Returns results that look something like this (but longer):

  Basic Info
    Ticket:               INC000000804714
    Summary:              Data Backup and Recovery
    Submitted:            Mon Feb 25 09:32:05 2008
    Status:               Closed
    [...]

This is the default mode.

=item remedy-ticket INC000000804714 full

Offers a basic "debugging" mode for the ticket itself, so that all fields 
in the ticket can be viewed.  Returns results like this:

  Full information for INC000000804714

             1 Entry ID                         INC000000804609
             2 Submitter                        RemedyAPIUser
             3 Submit Date                      1203960725
             4 Assignee Login ID                jared14
             5 Last Modified By                 AR_ESCALATOR
             6 Last Modified Date               1209327132
    [...]

=item remedy-ticket INC000000804714 worklog

Offers the same kind of "debugging" mode, but for the worklog information 
regarding the ticket.  Returns results like this:

Worklog Entries for INC000000804714

  Entry 1
             1 Work Log ID                      WLG000000809377
             2 Submitter                        spyksma
             3 Submit Date                      1204150081
             4 Assigned To                      (none)
      [...]
   

  Entry 2
             1 Work Log ID                      WLG000000810435
             2 Submitter                        cbyee
             3 Submit Date                      1204229267
             4 Assigned To                      (none)
      [...]

=item remedy-ticket INC000000804714 audit

Prints all of the audit log information about the ticket.  

=back

=cut

=head1 REQUIREMENTS

B<Stanford::Remedy>

=cut

=head1 AUTHOR

Based on original unattributed source script 'show_inc_tkt'.  Rewritten and new
features added by Tim Skirvin <tskirvin@stanford.edu>

=head1 LICENSE

For Stanford internal use only.

=cut
