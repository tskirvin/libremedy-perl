#!/usr/bin/perl -T
# Copyright and license are in the documentation below.

##############################################################################
### Configuration
##############################################################################

## Modify and uncomment this to use user code instead of just system-wide
## modules, or to use development versions.
# use lib '/FULL/PATH/TO/PERL/MODULES';
# use lib "/afs/ir/users/t/s/tskirvin/work/remedy/Stanford-Remedy/lib";   # not ideal
use lib "/afs/ir/users/t/s/tskirvin/work/libremedy-perl/lib";

## Shared configuration file.  Set with '--config'
our $CONFIG = "/etc/remedy/config";

## Number of days to go back for the 'unresolved' search. 
our $DAYS = 0;

## Print debugging information?  Set with '--debug'
our $DEBUG_LEVEL = 0;

## Default group information.  Related to, but not set by, '--group'.
our $DEFAULT_GROUP = '';

## Default user information.  Related to, but not set by, '--user'.
our $DEFAULT_USER = $ENV{'REMOTE_USER'} || $ENV{'USER'} || "unknown";

## Default request type, for convenience
our $DEFAULT_TYPE = "base";

## Tag to include include in the common log file
our $LOG_TAG = "REMEDY-TKT";

## Default ticket status for searching.
our $STATUS = 'open';

## In the 'time' functions, default number of minutes spent.
our $TIME = 0;

##############################################################################
### Declarations
##############################################################################

use strict;
use warnings;

use Data::Dumper;

use Getopt::Long;
# use Log::Log4perl qw/get_logger :no_extra_logdie_message/;
use Log::Log4perl qw/get_logger/;
use Pod::Usage;
use Remedy;
use Remedy::Ticket;
use Text::Wrap;

$0 =~ s%.*/%%g;     # Lose the annoying path information
$|++;               # Flush output

use vars qw/$USER $GROUP/;

##############################################################################
### main ()
##############################################################################

my $logger = get_logger ('');
$logger->all ('if this shows anywhere, we have a problem');

my $parser = new Getopt::Long::Parser;
my $result = $parser->getoptions (
    'c|config=s'    => \$CONFIG,
    'time=s'        => \$TIME,
    'days=s'        => \$DAYS,
    'user=s'        => \$USER,
    'group=s'       => \$GROUP,
    'status=s'      => \$STATUS,
    'd|debug+'      => \$DEBUG_LEVEL,
    'man'           => sub { pod2usage (-verbose=>2, -noperldoc => 1) },
    'h|help'        => sub { pod2usage (-verbose=>1) }) || error_usage ();
map { untaint ($_) } $CONFIG, $TIME, $DAYS, $USER, $GROUP, $STATUS;

my $remedy = Remedy::Ticket->connect ($CONFIG)
    or $logger->logdie ("couldn't connect to database: $@");
$logger->logdie ($@) if $@;

if ($DEBUG_LEVEL) {
    $logger->more_logging ($DEBUG_LEVEL) if $DEBUG_LEVEL >= 1;
}

my ($query, @rest) = @ARGV;
$query ||= '(none)';

if (lc $query eq 'ticket') { 
    my ($inc, $type, @args) = @rest;
    my $tkt = get_ticket ($remedy, $inc) or $logger->logdie ('no ticket');

    $type ||= '';
    my @list = qw/primary requestor assignee description resolution worklog/;
    if    (lc $type eq 'debug')      { @list = qw/debug/           } 
    elsif (lc $type eq 'audit')      { @list = qw/primary audit/   }
    elsif (lc $type eq 'worklog')    { @list = qw/primary worklog/ }
    elsif (lc $type eq 'timelog')    { @list = qw/primary timelog/ }
    elsif (lc $type eq 'summary')    { @list = qw/primary summary/ }
    elsif ($type =~ /^(all|full)$/i) { push @list, qw/audit time/  }
    else                             { push @list, qw/summary/     }

    print scalar $tkt->print_text (@list);
    exit 0;




} elsif (lc $query eq 'list') { 
    my ($type, $user, @extra) = @rest;
    $type ||= 'assign';
    $type = lc $type;

    my (%hash, @text);

    $user ||= $USER || $DEFAULT_USER;

    $hash{'user'}   = $USER   if defined $USER;
    $hash{'group'}  = $GROUP  if defined $GROUP;
    $hash{'status'} = $STATUS if defined $STATUS;

    if      ($type eq 'all') {
        
    } elsif ($type eq 'assign') {
        $hash{'assigned_user'} = $user;
        push @text, "assigned to '$user'";
        $user = '';

    } elsif ($type eq 'submit') {
        $hash{'submitted_user'} = $user;
        $user = '';
        push @text, "submitted by '$user'";

    } elsif ($type eq 'unassigned') { 
        $hash{'unassigned'}++;
        push @text, "unassigned tickets";
        
        if ($user) {
            $hash{'submitted_user'} = $user;
            $user = '';
            push @text, "submitted by '$user'";
        }

    } elsif ($type eq 'unresolved') { 
        $hash{'status'} = 'open';
        push @text, "open tickets";

    } else { error_usage ("invalid list type: '$type'") }


    if ($DAYS) {
        $hash{'last_modify_before'} = time - $DAYS * 86400;
        push @text, "unmodified for $DAYS days";
    }

    if ($user) { 
        my %person_search = ('SUNET ID' => $user);
        my @return = $remedy->read ('people', %person_search);
        $logger->logdie ("user '$user' does not belong to any groups")
            unless scalar @return;
        my @groups;
        foreach (@return) { push @groups, $_->group }

        if (scalar @groups) {
            push @text, "in all member groups of '$user'";
            $hash{'groups'} = \@groups;
        }
    }

    $logger->info (sprintf ("finding tickets where %s", join (", ", @text)));
    foreach my $tkt ($remedy->read ('ticket', %hash)) { 
        print scalar $tkt->summary_text;
    }
    exit 0;


} elsif (lc $query eq 'assign') { 
    my ($inc, @args) = @rest;
    my $tkt = get_ticket ($remedy, $inc) || error ("no ticket");

    error ("Must set a user (may be blank), a group, or both") 
        unless ($GROUP || defined $USER);
    
    my $return = $tkt->assign ('user' => $USER, 'group' => $GROUP);
    if ($return) { 
        print "success $return\n";
        print scalar $tkt->print_text;
        # print assignee information
    } else { 
        print "failure\n";
    }
    # ...and don't forget to add the 'set status' option!




} elsif (lc $query eq 'time_spent') { 
    my ($inc, $minutes) = @rest;
    my $tkt = get_ticket ($remedy, $inc) || error ("no ticket");

    $logger->info ("Adding timelog information to ticket " . $tkt->inc_num);

    # $tkt->time_spent ($minutes)
    my $timelog = $tkt->timelog_create ();
    $timelog->time_spent ($minutes);
    $timelog->submitter ($USER || $DEFAULT_USER);

    # ...then add it to the main tkt entry as well

    print scalar $timelog->print_text;
    if (eval { my $ret = $timelog ->save }) {
        print "Timelog entry " . $timelog->id . " saved\n";        
        exit 0;
    } else {
        error ("Error saving timelog entry: $@");
    }




} elsif (lc $query eq 'worklog') { 
    my ($inc, $text) = @rest;
    my $incnum = $remedy->parse_incident_number ($inc)
        or pod2usage (-verbose => 1, "Invalid ticket number: $inc");
    pod2usage (-verbose => 1) unless defined $incnum;

    $logger->info ("Adding worklog information to ticket $incnum");
    my $tkt = $remedy->incident ($incnum)
        or $remedy->die_error ("Couldn't load $incnum", $remedy->error);

    my $worklog = $tkt->worklog_create ();

    # all of this data needs a bit of work, but it's reasonably accurate
    $worklog->details ($text);
    $worklog->submitter ($USER || $DEFAULT_USER);
    $worklog->description ("submitted through script '$0'");
    $worklog->type ('General Information');     # probably ought to come from something else

    $worklog->time_spent ($TIME); 
    # care about 'time' (3)?

    print scalar $worklog->print_text;
    if (eval { my $ret = $worklog->save }) {
        print "Worklog entry " . $worklog->id . " saved\n";        
        exit 0;
    } else {
        error ("error saving worklog entry: $@");
    }

} elsif (lc $query eq 'help') {
    pod2usage (-verbose => 2, -exit_status => 0, -noperldoc => 1);

} else { error_usage ("invalid query type: '$query'") }

exit 0;

##############################################################################
### Subroutines 
##############################################################################

sub error_usage { 
    my ($error) = @_;
    my $logger = get_logger ('');
    $logger->error ($error);
    pod2usage (-exit_status => 2, -verbose => 1);
}

sub get_ticket {
    my ($remedy, $inc) = @_;
    my $logger = $remedy->logger_or_die ('no logger when getting ticket');
    $logger->info ("getting ticket '$inc'");
    my $tkt = eval { $remedy->get_ticket ($inc) };
    $logger->logdie ("error loading '$inc': $@") if $@ || !$tkt;
    return $tkt;
}

### untaint (TEXT, MATCH)
# Untaint user-offered data that is "safe enough", or die if it's not.
sub untaint {
    my ($text, $match) = @_;
    $match ||= '[a-zA-Z0-9_./: \-+=]*';
    return '' unless defined $text;
    if ($text =~ m%^($match)$%) { return $1 } 
    else                        { die ("tainted data: '$text' ($match)\n") }
}

##############################################################################
### Documentation
##############################################################################

=head1 NAME

remedy-ticket - manage remedy tickets

=head1 SYNOPSIS

B<remctl tools remedy ticket> 
  remedy-ticket [-hv] C<INCIDENT_NUMBER> [<base|full|worklog|audit>]

=head1 DESCRIPTION

remedy-ticket queries the Remedy database for information on a given ticket 
(as refrenced with C<INCIDENT_NUMBER> and creates a human-parsable report 
containing all relevant information, including worklog information.  It is 
designed to be used through a remctl interface for the Unix Systems team.

=head1 OPTIONS

=over 4

=item B<-h>, B<--help>

Prints out the synopsis information and exits.

=item B<-v>, B<--version>

Prints out the version information and exits.

=back

=head1 EXAMPLES

There are three basic modes of operation:

=over 4

=item remedy-ticket INC000000804714

=item remedy-ticket INC000000804714 base

Prints basic information about the ticket, the requestor, who it has been
assigned to, resolution information, and a summary of the associated work
log entries.  Returns results that look something like this (but longer):

  Basic Info
    Ticket:               INC000000804714
    Summary:              Data Backup and Recovery
    Submitted:            Mon Feb 25 09:32:05 2008
    Status:               Closed
    [...]

This is the default mode.

=item remedy-ticket INC000000804714 full

Offers a basic "debugging" mode for the ticket itself, so that all fields 
in the ticket can be viewed.  Returns results like this:

  Full information for INC000000804714

             1 Entry ID                         INC000000804609
             2 Submitter                        RemedyAPIUser
             3 Submit Date                      1203960725
             4 Assignee Login ID                jared14
             5 Last Modified By                 AR_ESCALATOR
             6 Last Modified Date               1209327132
    [...]

=item remedy-ticket INC000000804714 worklog

Offers the same kind of "debugging" mode, but for the worklog information 
regarding the ticket.  Returns results like this:

Worklog Entries for INC000000804714

  Entry 1
             1 Work Log ID                      WLG000000809377
             2 Submitter                        spyksma
             3 Submit Date                      1204150081
             4 Assigned To                      (none)
      [...]
   

  Entry 2
             1 Work Log ID                      WLG000000810435
             2 Submitter                        cbyee
             3 Submit Date                      1204229267
             4 Assigned To                      (none)
      [...]

=item remedy-ticket INC000000804714 audit

Prints all of the audit log information about the ticket.  

=back

=cut

=head1 REQUIREMENTS

B<Stanford::Remedy>

=cut

=head1 AUTHOR

Based on original unattributed source script 'show_inc_tkt'.  Rewritten and new
features added by Tim Skirvin <tskirvin@stanford.edu>

=head1 LICENSE

For Stanford internal use only.

=cut
