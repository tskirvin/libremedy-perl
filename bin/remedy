#!/usr/bin/perl -T
# Copyright and license are in the documentation below.

##############################################################################
### Configuration
##############################################################################

## Modify and uncomment this to use user code instead of just system-wide
## modules, or to use development versions.
# use lib '/FULL/PATH/TO/PERL/MODULES';
use lib "/afs/ir/users/t/s/tskirvin/work/remedy/Stanford-Remedy/lib"; 
use lib "/afs/ir/users/t/s/tskirvin/work/libremedy-perl/lib";

## Shared configuration file.  Set with '--config'
our $CONFIG = "/etc/remedy/config";

## Print debugging information?  Set with '--debug', can be set many times
our $DEBUG_LEVEL = 0;

## Defaults for 'userinfo' and 'groupinfo'
our $DEFAULT_USER  = $ENV{'REMOTE_USER'} || $ENV{'USER'} || "unknown";
our $DEFAULT_GROUP = '';

## Tag to include include in the common log file
our $LOG_TAG = "REMEDY-INFO";

## Will be overridden by '--user' and '--group'
use vars qw/$USER $GROUP/;

##############################################################################
### Declarations
##############################################################################

use strict;
use warnings;

use Getopt::Long;
use Log::Log4perl qw/get_logger :no_extra_logdie_message/;
use Pod::Usage;
use Remedy;
use Remedy::Form::People;
use Text::Wrap;

$0 =~ s%.*/%%g;     # Lose the annoying path information
$|++;               # Flush output

##############################################################################
### main ()
##############################################################################

my $parser = new Getopt::Long::Parser;
my $result = $parser->getoptions (
    'c|config=s'    => \$CONFIG,
    'user=s'        => \$USER,
    'group=s'       => \$GROUP,
    'd|debug+'      => \$DEBUG_LEVEL,
    'man'           => sub { pod2usage (-verbose=>2, -noperldoc => 1) },
    'h|help'        => sub { pod2usage (-verbose=>1) }) || error_usage ();

map { untaint ($_) } $CONFIG, $USER, $GROUP;

my $config = eval { Remedy::Config->load ($CONFIG) };
die "$@\n" unless $config;
die "$config\n" if $@;

# Still want it to log the fact that we ran to the logfile!

my $logger = $config->log->logger;

my ($query, @rest) = @ARGV;
$query ||= '(none)';

$logger->debug ("Connecting to database");
my $remedy = eval { Remedy->connect ($config) }
    or $logger->logdie ("couldn't connect to database: $@");
$logger->logdie ($@) if $@;

if (lc $query eq 'table') {   
    my ($table) = @rest;
    error_usage ('need a table name') unless $table;

    $logger->info ("Getting all information about table '$table'");
    foreach my $obj ($remedy->form ($table)) {
        if (defined $obj) { 
            print scalar $obj->debug_table;
            exit 0;
        } else {
            print "No information for '$table'; known values:\n";
            foreach (sort $remedy->registered_classes) { print " * $_\n" }
            exit 1;
        }
    }

} elsif (lc $query eq 'userinfo') { 
    my ($user) = @rest;
    $user ||= $USER || $DEFAULT_USER;
    error_usage ('need a user name') unless $user;

    my %search = ('SUNET ID' => $user);
    $logger->info ("Printing user information for '$user'");
    my @return = $remedy->read ('people', %search);
    if (! scalar @return) { 
        $logger->logdie ("no matching entries") 
    }
    foreach my $obj (@return) { 
        my $print = scalar $obj->print_text;
        print $print;
        if ($DEBUG_LEVEL) {
            my $debug = scalar $obj->debug_text;
            if ($debug ne $print) { print "\nRaw Data:\n$debug" }
        } 
    }

} elsif (lc $query eq 'groupinfo') {
    my ($group) = @rest;
    $group ||= $GROUP || $DEFAULT_GROUP;
    error_usage ('need a group name') unless $group;

    my %search = ('Support Group Name' => $group);
    $logger->info ("Printing group information for '$group'");
    foreach my $obj ($remedy->read ('supportgroup', %search)) { 
        print scalar ($DEBUG_LEVEL ? $obj->debug_text 
                                   : $obj->print_text);
    }

} elsif (lc $query eq 'help') {
    pod2usage (-verbose => 2, -exit_status => 0, -noperldoc => 1);

} else { error_usage ("invalid query type: '$query'") }

exit 0;

##############################################################################
### Subroutines 
##############################################################################

### error_usage (ERROR)
# Uses Pod::Usage to print an error message and the usage information
sub error_usage { 
    my ($error) = @_;
    pod2usage (-exit_status => 2, -verbose => 1, -message => $error) 
}

### untaint (TEXT, MATCH)
# Untaint user-offered data that is "safe enough", or die if it's not.
sub untaint {
    my ($text, $match) = @_;
    $match ||= '[a-zA-Z0-9_./: -]*';
    return '' unless defined $text;
    if ($text =~ m%^($match)$%) { return $1 } 
    else                        { die "tainted data: '$text' ($match)\n" }
}

##############################################################################
### Documentation
##############################################################################

=head1 NAME

remedy - prints information about the Remedy database

=head1 SYNOPSIS

B<remedy> table I<TABLE>

B<remedy> userinfo I<USERNAME>

B<remedy> groupinfo I<GROUPNAME>

B<remedy> --help

=head1 DESCRIPTION

B<remedy> pulls and prints information about the Remedy database, or about the
users and support groups managed within.

=head1 OPTIONS

=head2 MODES

=over 4

=item table I<TABLE>

Pulls and prints information about the table described in the perl module
I<TABLE>.  Loads the module first, so that we can load arbitrary installed
modules.

=item userinfo I<USERNAME>

Prints information about the user I<USERNAME>, including the full name, the
user's department and phone number, and a count and listing of its associated 
support groups.

=item groupinfo I<GROUPNAME>

Prints information about the support group I<GROUPNAME>, including the group
name, its associated email addresses, and a count and listing of its members.

=item help

Prints a basic help message and exits.

=back

=head2 FLAGS

=over 4

=item B<-c> I<CONFIG>, B<--config> I<CONFIG>

Loads the specified configuration file with B<Remedy::Config> instead of the
default.

=item B<-d>, B<--debug>

Print debugging information, both within this module and within the Remedy
module.

=item B<--group> I<GROUP>

Sets the group that we'll use with the I<groupinfo> query.  No default.

=item B<-h>, B<--help>

Prints out the synopsis information and exits.

=item B<--man>

Prints out the full help documentation and exits.

=item B<--user> I<USER>

Sets the user to run the I<userinfo> query about.  Defaults to
$ENV{'REMOTE_USER'} (if set by remctl) or $ENV{'USER'} (if set by 

=back

=head1 EXAMPLES

=over 4

=item remedy table Remedy::Person

Prints out all information about the table I<Remedy::Person>

=item remedy userinfo tskirvin

Prints out information about username I<tskirvin>.

=item remedy groupinfo 'ITS Unix Systems'

Prints out information about group I<ITS Unix Systems>. 

=back

=cut

=head1 FILES

=over 4

=item I</etc/remedy/config>

Remedy configuration file, parsed by B<Remedy::Config>.  

=back

=head1 REQUIREMENTS

B<Remedy>, B<Remedy::Form::People>

=head1 SEE ALSO

B<Remedy::Config>, B<Remedy::Form>, B<Remedy::Form::SupportGroup>

=head1 AUTHOR

Tim Skirvin <tskirvin@stanford.edu>

=head1 LICENSE

For Stanford internal use only.

=cut
